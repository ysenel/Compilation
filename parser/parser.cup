import java_cup.runtime.*;
import java.lang.Number;
import java.util.Vector;
import java.io.*;
import java.util.*;

parser code {:

public void syntax_error(String message, Object o, int line, int column){
	String msg2 = new StringBuilder().append("ligne : ").append(line+1).append(",").append(column)
					.append(",\t erreur : ").append(message)
					.append("\n\t-> symbole : ").append(o).toString();
	report_error(msg2, null) ;
}
public void syntax_error(String message, Object o){
	String msg2 = new StringBuilder().append("objet : ").append(o).append(" ->\t ").append(message).toString();
	report_error(msg2, null) ;
}
public void unrecovered_syntax_error(Symbol cur_token, String message){
	try{
		report_fatal_error(message, null) ;
	}catch (Exception e){
		
	}
}
public void unrecovered_syntax_error(Symbol cur_token){
	try{
		report_fatal_error("Erreur fatale", null) ;
	}catch (Exception e){
		
	}
}

public Tree tree = new Tree();
public  ArrayList<Node> caseArray = new ArrayList<Node>();
public SymboleTable symboleTable = new SymboleTable();

:}


terminal LPAR, RPAR, PLUS, MINUS, TIMES, DIVIDE, MINUS_U;
terminal NOT, AND, OR;
terminal EQ, EQUAL, EQUAL_NOT, EQUAL_INF, EQUAL_SUP, INF, SUP;
terminal SEMIC;
terminal LBRACKET, RBRACKET, DOTDOT, COMA, QUOTE, LBRACE, RBRACE;
terminal INTEGER, BIGINTEGER, UINTEGER, UBIGINTEGER, BOOL, CHAR, REAL, ENUM;
terminal ARROW, STRING;
terminal SWITCH, CASE, WHILE, DO, OD, END_SWITCH;
terminal String TRUE;
terminal String FALSE ;
terminal String NUMBER;
terminal String STRINGCONTENT;

non terminal Node begin, variable, val, expr, loop, switchCase;
non terminal Tree begining;
non terminal ArrayList<Node> instructions, cases;
non terminal enumeration, intervals, array;
non terminal Node prop;
non terminal MyEnumType type;

precedence left PLUS, MINUS, EQUAL, EQUAL_NOT, EQUAL_INF, EQUAL_SUP, SUP, INF;
precedence left TIMES, DIVIDE, AND, OR;
precedence left MINUS_U, NOT;

start with begining;

begining ::=
	begin
		{:
		System.out.println(parser.tree.TacToString());
		:}
	;

begin ::=
	variable:var begin
		{: Tree.node.add(var); :}
	|switchCase:s begin
		{: Tree.node.add(s); :}
	|loop:l begin
		{: Tree.node.add(l); :}
	|
	;

prop ::=
	prop:p1 AND prop:p2 {: RESULT = new NodeArithmetic(MyEnumType.AND, p1, p2); :}
	|prop:p1 OR prop:p2 {: RESULT = new NodeArithmetic(MyEnumType.OR, p1, p2); :}
	|prop:p1 EQUAL prop:p2 {: RESULT = new NodeArithmetic(MyEnumType.EQUAL, p1, p2); :}
	|expr:e1 EQUAL expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.EQUAL, e1, e2); :}
	|prop:p1 EQUAL_NOT prop:p2 {: RESULT = new NodeArithmetic(MyEnumType.EQUAL_NOT, p1, p2); :}
	|expr:e1 EQUAL_NOT expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.EQUAL_NOT, e1, e2); :}
	|expr:e1 SUP expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.SUP, e1, e2); :}
	|prop:p1 SUP prop:p2 {: RESULT = new NodeArithmetic(MyEnumType.SUP, p1, p2); :}
	|prop:p1 INF prop:p2 {: RESULT = new NodeArithmetic(MyEnumType.INF, p1, p2); :}
	|expr:e1 INF expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.INF, e1, e2); :}
	|prop:p1 EQUAL_SUP prop:p2 {: RESULT = new NodeArithmetic(MyEnumType.EQUAL_SUP, p1, p2); :}
	|expr:e1 EQUAL_SUP expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.EQUAL_SUP, e1, e2); :}
	|prop:p1 EQUAL_INF prop:p2 {: RESULT = new NodeArithmetic(MyEnumType.EQUAL_INF, p1, p2); :}
	|expr:e1 EQUAL_INF expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.EQUAL_INF, e1, e2); :}
	|LPAR prop:p RPAR {: RESULT = p; :}
	|NOT prop:p {: RESULT = new NodeArithmetic(MyEnumType.NOT, p, null); :}
	|TRUE:t {:RESULT = new NodeConstant(t.toString(), null);:}
	|FALSE:f {:RESULT = new NodeConstant(f.toString(), null);:}
	;

expr ::=
    expr:e1 PLUS expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.PLUS, e1, e2); :}
	|expr:e1 MINUS expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.MINUS, e1, e2); :}   
    |expr:e1 TIMES expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.TIMES, e1, e2); :}
	|expr:e1 DIVIDE expr:e2 {: RESULT = new NodeArithmetic(MyEnumType.DIVIDE, e1, e2); :}    
    //|LPAR expr:e RPAR {: :}    
    |MINUS expr:e {: :} %prec MINUS_U 
	|val:v {: RESULT = v; :}
	|STRINGCONTENT:v ARROW	{: RESULT = new NodePointer(null, v, null); :}		
    ;

variable ::= 
	type:t STRINGCONTENT:name EQ expr:val SEMIC
		{: 
			if(!parser.symboleTable.exist(name)){
				parser.symboleTable.addVariable(t, name);
				RESULT = new NodeVariable(t, name, val.getValue(), val);
			}
			else
				RESULT = new NodeError(EnumTac.VAR_ALREADY_DEF, name, nameleft+1, nameright);

		:}
	|type:t STRINGCONTENT:name SEMIC
		{:
			if(!parser.symboleTable.exist(name)){
				parser.symboleTable.addVariable(t, name);
				RESULT = new NodeVariable(t, name, null, null); 
			}
			else
				RESULT = new NodeError(EnumTac.VAR_ALREADY_DEF, name, nameleft+1, nameright);
		:}
	|STRINGCONTENT:name EQ STRINGCONTENT:val SEMIC
		{:	
			if(parser.symboleTable.exist(name) && parser.symboleTable.exist(val)){
				RESULT = new NodeVariable(null, name, val, null); 
			}
			else{
				RESULT = new NodeError(EnumTac.VAR_UNDECLARED, name, nameleft+1, nameright);
			}
		:}
	|STRINGCONTENT:name EQ NUMBER:val SEMIC
		{: 	
			if(parser.symboleTable.exist(name)){
				RESULT = new NodeVariable(null, name, val, null); 
			}
			else
				RESULT = new NodeError(EnumTac.VAR_UNDECLARED, name, nameleft+1, nameright);
		:}
	|type:t STRINGCONTENT:name EQ TRUE:val SEMIC
		{: 
			if(!parser.symboleTable.exist(name)){
				parser.symboleTable.addVariable(t, name);
				RESULT = new NodeVariable(t, name, val, null);
	    	}
	    	else
	    		RESULT = new NodeError(EnumTac.VAR_ALREADY_DEF, name, nameleft+1, nameright);
	    :}
	|type:t STRINGCONTENT:name EQ FALSE:val SEMIC
		{: 	
			if(!parser.symboleTable.exist(name)){
				parser.symboleTable.addVariable(t, name);
				RESULT = new NodeVariable(t, name, val, null);
	    	}
	    	else
	    		RESULT = new NodeError(EnumTac.VAR_ALREADY_DEF, name, nameleft+1, nameright); 
		:}
	|type:t STRINGCONTENT:name EQ QUOTE STRINGCONTENT:val QUOTE SEMIC
		{: 
			if(!parser.symboleTable.exist(name)){
				parser.symboleTable.addVariable(t, name);
				RESULT = new NodeVariable(t, name, val, null);
	    	}
	    	else
	    		RESULT = new NodeError(EnumTac.VAR_ALREADY_DEF, name, nameleft+1, nameright);
		:}
	|type:t STRINGCONTENT:name EQ ARROW STRINGCONTENT:val SEMIC
		{:
			if(!parser.symboleTable.exist(name) && parser.symboleTable.exist(val) ){
				parser.symboleTable.addVariable(t, name);
				RESULT = new NodePointer(t, name, val); 
	    	}
	    	else
	    		RESULT = new NodeError(EnumTac.VAR_ALREADY_DEF, name, nameleft+1, nameright);
		:}
	|type:t STRINGCONTENT:name ARROW SEMIC
		{:
			if(!parser.symboleTable.exist(name)){
				parser.symboleTable.addVariable(t, name);
				RESULT = new NodePointer(t, name, null); 
	    	}
	    	else
	    		RESULT = new NodeError(EnumTac.VAR_ALREADY_DEF, name, nameleft+1, nameright);
		:}
	|STRINGCONTENT:name EQ ARROW STRINGCONTENT:val SEMIC
		{:
			if(parser.symboleTable.exist(name)){
				RESULT = new NodePointer(null, name, val); 
	    	}
	    	else
	    		RESULT = new NodeError(EnumTac.VAR_ALREADY_DEF, name, nameleft+1, nameright);
		:}
	// non fait
	|type STRINGCONTENT:var EQ LPAR enumeration:val RPAR SEMIC
	|intervals SEMIC
	|array SEMIC
	;

loop ::=
	WHILE LPAR prop:p RPAR DO instructions:i OD
		{:
			RESULT = new NodeWhile(p, i);
		:}
	;



instructions ::=
	variable:v instructions:i
		{: 
			i.add(v); 
			RESULT = i;
		:}
	|switchCase:s instructions:i
		{: 
			i.add(s); 
			RESULT = i;
		:}
	|loop:l instructions:i
		{:
			i.add(l); 
			RESULT = i;
		:}
	|
		{: 
			RESULT = new ArrayList<Node>(); 
		:}
	;

switchCase ::= 
	SWITCH cases:c END_SWITCH 
		{:
			RESULT = new NodeSwitch(c);
		:}
	;

cases ::=
	CASE LPAR prop:p RPAR instructions:i cases:c
		{:
			c.add(new NodeCase(p, i));
			RESULT = c;
		:}

	|
		{:RESULT = new ArrayList<Node>();:}
	;


array ::=
	type STRINGCONTENT LBRACKET NUMBER RBRACKET
	;

intervals ::=
	LBRACKET NUMBER DOTDOT NUMBER RBRACKET
	;

enumeration ::=
	NUMBER enumeration
	|COMA enumeration
	|
	;

val ::=
	NUMBER:val1		{: RESULT = new NodeConstant(val1, null); :}
	|STRINGCONTENT:val2		{: RESULT = new NodeConstant(val2, null); :}
	;

type ::=
	INTEGER		{: RESULT = MyEnumType.INTEGER; :}
	|BIGINTEGER		{: RESULT = MyEnumType.BIGINTEGER; :}
	|UINTEGER		{: RESULT = MyEnumType.UINTEGER; :}
	|UBIGINTEGER		{: RESULT = MyEnumType.UBIGINTEGER; :}
	|BOOL		{: RESULT = MyEnumType.BOOLEAN; :}
	|CHAR		{: RESULT = MyEnumType.CHARACTER; :}
	|REAL		{: RESULT = MyEnumType.REAL; :}
	|ENUM		{: RESULT = MyEnumType.ENUM; :}
	|STRING		{: RESULT = MyEnumType.STRING; :}
	;
